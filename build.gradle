plugins {
    id 'java'
	id 'com.gradleup.shadow' version '9.1.0'
}

def RELEASE_TARGET = file("${rootProject.rootDir}/build/release")

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
	toolchain {
		// xxx use system unless errors occur
		//languageVersion = JavaLanguageVersion.of(11)
	}
}

// SOURCE: https://github.com/GradleUp/shadow/issues/644
// run `shadowJar` task only if that was user's preference, i.e. if user explicitly
// asked for it by manually specifying it via CLI by specifying that shadowJar
// task should be ran
def shadowJarEnabled = gradle.startParameter.taskRequests
  .find {it.args.find {it.contains("shadowJar")} != null  } != null
if (!shadowJarEnabled) {
  gradle.startParameter.excludedTaskNames += "shadowJar"
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.runelite.net'
    }
    mavenCentral()
}

def runeLiteVersion = '1.12.6'

dependencies {
    compileOnly group: 'net.runelite', name: 'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name: 'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name: 'jshell', version: runeLiteVersion

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation 'junit:junit:4.13.2'
}

group = 'com.aggeplugins'
//version = '1.0.0'

// utf-8 encoding
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
	archiveBaseName = 'RuneliteHijack'
}

tasks {
	shadowJar {
		archiveClassifier = 'shaded'

		mergeServiceFiles()

		doLast {
			copy {
				from(archiveFile)
				into(RELEASE_TARGET)
			}
		}
	}
}
